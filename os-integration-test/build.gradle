plugins {
    id 'groovy'
}

dependencies {
    compile project(':core')

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    testRuntime 'ch.qos.logback:logback-classic:1.1.2'
}

test {
    useJUnit {
        excludeCategories 'org.hidetake.groovy.ssh.test.os.RequireAgent'
        excludeCategories 'org.hidetake.groovy.ssh.test.os.RequirePrivilege'
    }
}

task testWithSudo(type: Test) {
    useJUnit {
        includeCategories 'org.hidetake.groovy.ssh.test.os.RequirePrivilege'
    }
    onlyIf { System.getenv('INTEGRATION_TEST_SSH_HOST') }
    onlyIf { System.getenv('INTEGRATION_TEST_SSH_USER') }
    onlyIf { System.getenv('INTEGRATION_TEST_SSH_KEY_PATH') }
}

task startSshAgent(type: Exec) {
    commandLine 'ssh-agent'
    standardOutput = new ByteArrayOutputStream()
    doLast {
        standardOutput.toString().eachMatch(~/(.+?)=(.+?);/) { all, k, v ->
            assert k in ['SSH_AGENT_PID', 'SSH_AUTH_SOCK']
            [addKeyToSshAgent, testWithAgent, stopSshAgent]*.environment(k, v)
        }
    }
}

task addKeyToSshAgent(type: Exec) {
    dependsOn startSshAgent
    commandLine 'ssh-add', "${System.getProperty('user.home')}/.ssh/id_rsa"
}

task stopSshAgent(type: Exec) {
    dependsOn startSshAgent
    commandLine 'ssh-agent', '-k'
}

task testWithAgent(type: Test) {
    useJUnit {
        includeCategories 'org.hidetake.groovy.ssh.test.os.RequireAgent'
    }
    dependsOn addKeyToSshAgent
    finalizedBy stopSshAgent
}

task testReport(type: TestReport) {
    destinationDir = testReportDir
    reportOn tasks.withType(Test)*.binResultsDir
}

tasks.withType(Test) {
    reports.html.enabled = false
    finalizedBy testReport
}

check.dependsOn tasks.withType(Test)
