allprojects {
    description = 'Groovy SSH library'
    group = 'org.hidetake'

    // Set product version if tag is given on Travis CI
    version = System.getenv('TRAVIS_TAG') ?: 'SNAPSHOT'

    ext.license = 'Apache-2.0'
    ext.licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    ext.vcsUrl = 'https://github.com/int128/groovy-ssh.git'

    repositories {
        jcenter()
    }
}


def configureTestExecutionOrder(String... projects) {
    def tasks = projects.collect { project -> tasks.getByPath(":$project:check") }.reverse()
    tasks.tail().inject(tasks.head()) { after, before ->
        after.dependsOn before
        before
    }
}

configureTestExecutionOrder('core', 'server-integration-test', 'cli', 'os-integration-test')
