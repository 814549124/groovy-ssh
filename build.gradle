plugins {
    id 'groovy'
    id 'jacoco'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '0.6'
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'com.jcraft:jsch:0.1.51'
	compile 'com.jcraft:jsch.agentproxy.connector-factory:0.0.7'
	compile 'com.jcraft:jsch.agentproxy.jsch:0.0.7'
    testCompile('org.spockframework:spock-core:0.7-groovy-2.0')
    testCompile 'cglib:cglib-nodep:3.1'
    testRuntime 'org.objenesis:objenesis:2.1'
    testRuntime 'ch.qos.logback:logback-classic:1.1.2'
    testCompile 'org.apache.sshd:sshd-core:0.12.0'
    testRuntime 'org.bouncycastle:bcpkix-jdk15on:1.51'
}

test {
    useJUnit {
        excludeCategories 'org.hidetake.groovy.ssh.server.ServerIntegrationTest'
    }
}

task serverIntegrationTest(type: Test) {
    useJUnit {
        includeCategories 'org.hidetake.groovy.ssh.server.ServerIntegrationTest'
    }
    reports {
        html.destination = file("$buildDir/reports/server-integration-tests")
    }
}

jacoco {
    toolVersion = '0.7.1.201405082137'
}

task jacocoServerIntegrationTestReport(type: JacocoReport) {
    executionData serverIntegrationTest
    sourceSets sourceSets.main
    reports.html.destination "$buildDir/reports/jacoco/server-integration-tests"
}

check.dependsOn serverIntegrationTest
check.dependsOn jacocoTestReport
check.dependsOn jacocoServerIntegrationTestReport

if (System.getProperty('os.name') == 'Linux') {
    tasks.withType(JavaForkOptions) {
        systemProperty 'java.security.egd', 'file:/dev/./urandom'
    }
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    from "${buildDir}/docs/groovydoc"
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name project.name
                    description project.description
                    url project.property('pom.url')
                    scm {
                        url project.property('pom.scm.url')
                        connection project.property('pom.scm.connection')
                    }
                    licenses {
                        license {
                            name project.property('pom.license.name')
                            url project.property('pom.license.url')
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id project.property('pom.developer.id')
                            name project.property('pom.developer.name')
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = project.properties.bintrayUser
    key = project.properties.bintrayKey
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.name
    }
}

tasks.bintrayUpload.doFirst {
    assert !project.version.contains('SNAPSHOT')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}
