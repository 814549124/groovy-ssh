plugins {
    id 'groovy'
}

dependencies {
    compile project(':core')
    compile project(':os-integration-test')

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'

    testRuntime 'ch.qos.logback:logback-classic:1.1.2'
}

task startSshAgent(type: Exec) {
    commandLine 'ssh-agent'
    standardOutput = new ByteArrayOutputStream()
    doLast {
        standardOutput.toString().eachMatch(~/(.+?)=(.+?);/) { all, k, v ->
            assert k in ['SSH_AGENT_PID', 'SSH_AUTH_SOCK']
            [addKeyToSshAgent, test, stopSshAgent]*.environment(k, v)
        }
    }
}

task addKeyToSshAgent(type: Exec, dependsOn: startSshAgent) {
    commandLine 'ssh-add', "${System.getProperty('user.home')}/.ssh/id_rsa"
}

task stopSshAgent(type: Exec, dependsOn: startSshAgent) {
    commandLine 'ssh-agent', '-k'
}

test.dependsOn addKeyToSshAgent
test.finalizedBy stopSshAgent
test.onlyIf { System.getenv('INTEGRATION_TEST_SSH_HOST') }
test.onlyIf { System.getenv('INTEGRATION_TEST_SSH_USER') }
