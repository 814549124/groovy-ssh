/*
 * Examples.
 * 
 * Prepare following to execute tasks in this example.
 * - start sshd
 * - generate key pair (use ssh-keygen)
 * - add your pubic key to authorized_keys
 * - make sure that you can log in without any password
 * 
 * Or, rewrite host or user as you like.
 * 
 */
import org.hidetake.gradle.ssh.Ssh

buildscript {
	repositories {
		mavenCentral()
		flatDir { dirs '../build/libs' }
	}

	dependencies {
		classpath 'org.hidetake:gradle-ssh-plugin:1.0'
		classpath 'com.jcraft:jsch:0.1.48'
	}
}

apply plugin: 'ssh'

ssh {
	remote {
		host = 'localhost'
		user = System.properties['user.name']
		identity = "${System.properties['user.home']}/.ssh/id_rsa"
	}
	config(StrictHostKeyChecking: 'no')
}

remotes {
	someServer {
		host = 'localhost' // TODO: change to your host name
		user = System.properties['user.name']
		identity = "${System.properties['user.home']}/.ssh/id_rsa"
	}
}

task defaultRemote(type: Ssh) {
	description 'execute a command on default remote'
	channel {
		command = 'env'
	}
}

task specifiedRemote(type: Ssh) {
	description 'execute a command on specified remote'
	remote = project.remotes.someServer
	channel {
		command = 'env'
	}
}

task concurrent(type: Ssh) {
	description 'execute commands concurrently on the remote'
	(1..5).each { i ->
		channel { command = "echo ${i} && sleep ${i} && echo ${i}" }
	}
}
