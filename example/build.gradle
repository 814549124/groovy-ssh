/*
 * Examples.
 * 
 * Prepare following to execute tasks in this example.
 * - start sshd
 * - generate key pair (use ssh-keygen)
 * - add your pubic key to authorized_keys
 * - make sure that you can log in without any password
 * 
 * Or, rewrite host or user as you like.
 * 
 */
buildscript {
	if (System.properties.useGitHubRelease) {
		repositories {
			mavenCentral()
		}
		dependencies {
			classpath 'org.hidetake:gradle-ssh-plugin:0.1.3'
		}
	} else {
		repositories {
			mavenCentral()
			flatDir { dirs '../build/libs' }
		}
		dependencies {
			classpath 'org.hidetake:gradle-ssh-plugin:latest.integration'
			classpath 'com.jcraft:jsch:0.1.48'
		}
	}
}

apply plugin: 'ssh'

ssh {
	config(StrictHostKeyChecking: 'no')
}

remotes {
	localhost {
		role 'webServers'
		host = 'localhost'
		user = System.properties['user.name']
		identity = file("${System.properties['user.home']}/.ssh/id_rsa")
	}
	myhost {
		role 'webServers'
		host = '127.0.0.1'
		user = System.properties['user.name']
		identity = file("${System.properties['user.home']}/.ssh/id_rsa")
	}
}

task showEnvironmentSingle(type: SshTask) {
	description 'execute command on a remote'
	session(remotes.localhost) {
		execute "echo ${remote.user}@${remote.host}:${remote.port}"
		execute 'env'
	}
}

task showEnvironmentAll(type: SshTask) {
	description 'execute command on remotes'
	session(remotes.role('webServers')) {
		execute 'env'
	}
}

task concurrentExecution(type: SshTask) {
	description 'describes how to execute SSH concurrently'
	session(remotes.localhost) {
		(1..5).each { i ->
			executeBackground "sleep ${i} && echo -n ${i}- && date"
		}
	}
	session(remotes.myhost) {
		executeBackground 'sar 1 5'
	}
}

task executeSudo(type: SshTask) {
	description 'execute command on a remote'
	session(remotes.localhost) {
		execute('sudo env', pty: true)
	}
}

task executionFailed(type: SshTask) {
	description 'describes error handling'
	session(remotes.localhost) {
		// may be failed
		execute 'touch /root/test'
	}
}

task sshInTask << {
	description 'describes how to execute SSH in the task'
	def command = "env"
	println command
	sshexec {
		session(remotes.localhost) {
			execute command
		}
	}
}

void showRemotes(Project baseProject) {
	logger.info('({}) rootProject.remotes = {}', baseProject, baseProject.parent.remotes)
	logger.info('({}) project.remotes = {}', baseProject, baseProject.remotes)
}
