= Migration Guide

== Migration from 2.6.x to 2.7.x

This section explains how to migrate from 2.6.x to 2.7.x.

=== No backward compatible change

==== Behavior of `ssh.run` method is changed

`ssh.run` method runs sessions in parallel if 2 or more sessions are given.

See the example.

[source,groovy]
----
ssh.run {
  session(remotes.server) { execute 'foo' }
  session(remotes.server) { execute 'bar' }
}
----

In 2.6.0 or earlier, `foo` is executed and then `bar` is executed.
Since 2.7.0, commands are executed in parallel.


==== Return type of `ssh.run` method is changed

Return type of `ssh.run` method has been changed as followings:

- If no session is given, the method returns null. (as-is)
- If a session is given, the method returns its result. (as-is)
- If sessions are given, the method returns a list of results. (changed since 2.7.0)

[source,groovy]
----
// run a session
ssh.run {
  session(remotes.server) { 'value' }
}
// -> 'value'

// run sessions
ssh.run {
  session(remotes.server) { 'value1' }
  session(remotes.server) { 'value2' }
}
// -> ['value1', 'value2']
----


==== Exception type of `ssh.run` method is changed

Exception type of `ssh.run` method has been changed as followings:

- If no session is given, the method never throw an exception. (as-is)
- If a session is given and it caused an error, the method throws the exception. (as-is)
- If sessions are given and 1 or more caused error(s), the method throws `ParallelSessionsException` which includes all exception(s). (changed since 2.7.0)


==== `executeBackground` is deprecated

`executeBackground` method has been deprecated and now same as `execute` method.
Please rewrite with `execute` in parallel as follows:

[source,groovy]
----
ssh.run {
  session(remotes.server) {
    executeBackground 'foo'
    executeBackground 'bar'
  }
}

// above should be rewrote to below since 2.7.0
ssh.run {
  session(remotes.server) {
    execute 'foo'
  }
  session(remotes.server) {
    execute 'bar'
  }
}
----


== Migration from 2.3.x to 2.4.x

This section explains how to migrate from 2.3.x to 2.4.x.

=== No backward compatible change

==== File transfer methods

Following undocumented methods have been removed.
Use map parameter methods instead.

[source,groovy]
----
// FIXME: no longer supported
put(InputStream stream, String remotePath)
put(File localFile, String remotePath)
put(String localPath, String remotePath)

get(String remotePath, OutputStream stream)
get(String remotePath, String localPath)
get(String remotePath, File localFile)

// Use followings instead
put from: something, into: remotePath
get from: remotePath, into: something
----


== Migration from 1.0.x to 1.1.x

This section explains how to migrate from 1.0.x to 1.1.x.

=== New features

==== Port forwarding

Port forwarding is supported now.
See <<Enable the port forwarding>>.

==== Map based DSL extension system

We can extend DSL with a map of method name and implementation.
Following example adds the method `restartAppServer`.

```groovy
ssh.settings {
  extensions.add restartAppServer: {
    execute "/opt/${project.name}/tomcat/bin/shutdown.sh"
    execute "/opt/${project.name}/tomcat/bin/startup.sh"
  }
}

ssh.run {
  session(ssh.remotes.testServer) {
    restartAppServer()
  }
}
```

=== No backward compatible change

==== Class based DSL extension system

Any extension classes in the build script will no longer work.
They must be placed in the `buildSrc/src/main/groovy` directory.

So we recommend to use the map based extension instead of the class based extension.

For example, following extension:

```groovy
// buildSrc/src/main/groovy/extensions.groovy
class TomcatExtension {
  def restartAppServer() {
    execute "/opt/${project.name}/tomcat/bin/shutdown.sh"
    execute "/opt/${project.name}/tomcat/bin/startup.sh"
  }
}
```

can be migrated to:

```groovy
// build.gradle
ssh.settings {
  extensions.add restartAppServer: {
    execute "/opt/${project.name}/tomcat/bin/shutdown.sh"
    execute "/opt/${project.name}/tomcat/bin/startup.sh"
  }
}
```


== Migration from 0.4.x to 1.0.x

This section explains how to migrate from 0.4.x to 1.0.x.

=== No backward compatible changes

`sshexec` is no longer supported. Use `ssh.run` instead.

```groovy
task example << {
  // FIXME: sshexec is no longer supported
  sshexec {
    session(...) {...}
  }

  // use ssh.run instead
  ssh.run {
    session(...) {...}
  }
}
```

`ssh {}` is no longer supported. Use `ssh.settings {}` instead.

```groovy
// FIXME: ssh is no longer supported
ssh {
  knownHosts = allowAnyHosts
}

// use ssh.settings instead
ssh.settings {
  knownHosts = allowAnyHosts
}
```


== Migration from 0.3.x to 0.4.x

This section explains how to migrate from 0.3.x to 0.4.x.

Since 0.4.0, core code has been separated to the SSH library
https://github.com/int128/groovy-ssh[groovy-ssh] and removed from the plugin.

It introduces new style which is common between the plugin and the SSH library.
It also causes no backward compatible changes.

Please let me know if there is any problem.

=== New style

==== Global settings

`ssh` method has been deprecated.

```groovy
ssh {
  // apply global settings here
}
```

Instead, use `ssh.settings`.

```groovy
ssh.settings {
  // apply global settings here
}
```

==== SSH execution

`SshTask` and `sshexec` method have been deprecated.

```groovy
// Deprecated
task testTask1(type: SshTask) {
  ssh {
    dryRun = true
  }
  session(remotes.webServer) {
    execute 'ls'
  }
}
```

```groovy
task testTask1 << {
  // Deprecated
  sshexec {
    ssh {
      dryRun = true
    }
    session(remotes.webServer) {
      execute 'ls'
    }
  }
}
```

Instead, use `ssh.run` method in the task.

```groovy
task testTask1 << {
  ssh.run {
    settings {
      // apply one-time settings here
      dryRun = true
    }
    session(ssh.remotes.webServer) {
      // describe operations here
      execute 'ls'
    }
  }
}
```

=== No backward compatible changes

==== Logging settings

Following settings have been removed.

[options="header"]
|===
|Key              | Type     | Description
|`outputLogLevel` | LogLevel | Log level of the standard output on the command or shell execution. Default is `LogLevel.QUIET`.
|`errorLogLevel`  | LogLevel | Log level of the standard error on the command or shell execution. Default is `LogLevel.ERROR`.
|===

Instead use `logging` setting to enable verbose logging.

[options="header"]
|===
|Key              | Type     | Description
|`logging`        | String   | If this is `slf4j`, console log of the remote command is sent to Gradle logger. If this is `stdout`, it is sent to standard output/error. If this is `none`, console logging is turned off. Defaults to `slf4j`.
|===

e.g.

```groovy
ssh.settings {
  logging = 'stdout'
}
ssh.run {
}
```
